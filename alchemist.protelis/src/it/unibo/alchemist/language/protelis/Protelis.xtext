grammar it.unibo.alchemist.language.protelis.Protelis with org.eclipse.xtext.common.Terminals
import "http://www.eclipse.org/emf/2002/Ecore" as ecore

generate protelis "http://alchemist.apice.unibo.it/Protelis"

Program: package=PackageDeclaration? (imports+=Import)* (definitions+=FunctionDef)* program=Block;

VarList: args += VAR (',' args += VAR)* ;

RepList: args+=RepInitialize (',' args+=RepInitialize)*;
ExprList: args+=Expression (',' args+=Expression)*;
RepInitialize: x=VAR '<-' w=VarVal;

PackageDeclaration:
	'package' name=JAVAPACKAGE
;

Import:
	'import' class=JAVACLASS'.' method=ATOM 'as' name=ATOM |
	'import' class=JAVACLASS'.' name=(ATOM|'*')
;

Block: (first=Statement) (';' others=Block)? ;

Statement: Assignment | LinkableStatement ;

Declaration:
	'let' name=VARNAME '=' right=Expression
;

LinkableStatement: Declaration | Expression;

Assignment:
	refVar=[LinkableStatement|VARNAME] name='=' right = Expression
;

Expression:
 	Logical ';'?
;

FunctionDef: 'def' name=ATOM '(' (args=VarList)? ')' '{' body=Block '}';

FunctionCall returns Expression:
	(function=[FunctionDef|ATOM] | name='#' method=ATOM )'('args=ExprList?')'
;

Lambda returns Expression: '(' (lambdaArgs=VarList)? ')' name='->' '{' body=Block '}';

Rep returns Expression: 
	name='rep' '(' init=RepList ')' '{' body=Block '}' 
;

If returns Expression:
	name='if' '(' cond=Expression ')' '{' then=Block '}' 'else' '{' else=Block '}'
;

NBR returns Expression:
	name='nbr' '(' arg=Expression ')' 
;

Builtin returns Expression:
	name='self' |
	name='nbrRange' |
	name='dt' |
	name='pi' |
	name='e' |
	name='inject' '(' refVar=[Expression|VARNAME] ')' |
	name='eval' '(' arg = ( VAR | StringVal ) ')' |
	name='alignedMap' '(' arg=Expression ',' cond=Expression ',' op=Expression ',' default=Expression ')' |
	name='mux' '(' cond=Expression ')' '{' then=Block '}' 'else' '{'  else=Block '}' |
	HoodOp
;

HoodOp returns Expression: (
	name='minHood' |
	name='maxHood' |
	name='anyHood' |
	name='allHood' |
	name='sumHood' |
	name='meanHood' |
	name='unionHood'
) inclusive='PlusSelf'? '(' arg=Expression ')'
;

Logical returns Expression:
 Equality ({Expression.left=current} name=('&&'|'||') right=Equality)*;

Equality returns Expression:
 Relational ({Expression.left=current} name=('=='|'!=') right=Relational)*;

Relational returns Expression:
 Addition ({Expression.left=current} name=('>'|'>='|'<'|'<=') right=Addition)*;

Addition returns Expression:
 Multiplication ({Expression.left=current} name=('+'|'-') right=Multiplication)*;

Multiplication returns Expression:
 Power ({Expression.left=current} name=('*'|'/'|'%') right=Power)*;

Power returns Expression:
 Prefix ({Expression.left=current} name=('^') right=Prefix)*;

Prefix returns Expression:
    Postfix | ({Prefix} name=('-'|'!') right=Postfix);

Postfix returns Expression:
    Primary
    ({Expression.left=current}(
    	(name='.' methodName=('apply' | ATOM) '(' (args=ExprList)? ')')
    ))?
;

Primary returns Expression:
 (v = (VarVal | FunctionCall | Rep | NBR | If) | '(' Expression ')')
;

VarVal returns Expression: VAR | Local;

Local returns Expression : Scalar | Builtin | Lambda;

VAR: name=VARNAME;

Scalar: DoubleVal | StringVal | BooleanVal | TupleVal;

DoubleVal: val=DOUBLE;

StringVal: val=STRING;

BooleanVal: val=BOOLEAN;

TupleVal: name='[' args = ExprList? ']';

VARNAME: ATOM;
JAVAPACKAGE : ATOM('.'ATOM)*;
JAVACLASS : UPPERCASE | JAVAPACKAGE'.'UPPERCASE;

DOUBLE returns ecore::EDouble: INT('.'INT)?('e'|'E''-'?INT)? | 'Infinity' | 'NaN';
BOOLEAN returns ecore::EBoolean: "true" | "false";

terminal ATOM : ('a'..'z'|'_')('a'..'z'|'A'..'Z'|'0'..'9'|'_')*;
terminal UPPERCASE : ('A'..'Z')('a'..'z'|'A'..'Z'|'0'..'9'|'_')*;
terminal ML_COMMENT	: '/*' -> '*/';
